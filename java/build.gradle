plugins {
    id 'java'
}

group = 'com.fixcraft'
version = '3.6.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
}

[compileJava, compileTestJava].forEach { task ->
    task.sourceCompatibility = '1.8'
    task.targetCompatibility = '1.8'
    task.options.encoding = 'UTF-8'
}

jar {
    if (delegate.metaClass.hasProperty(delegate, 'duplicatesStrategy')) {
        duplicatesStrategy = 'exclude'
    }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    if (delegate.metaClass.hasProperty(delegate, 'archiveFileName')) {
        archiveFileName = 'basefwx-java.jar'
    } else if (delegate.metaClass.hasProperty(delegate, 'archiveName')) {
        archiveName = 'basefwx-java.jar'
    }
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    manifest {
        attributes 'Main-Class': 'com.fixcraft.basefwx.cli.BaseFwxCli'
    }
}
